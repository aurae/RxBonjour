buildscript {
    repositories {
        jcenter()
        google()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$GRADLE_PLUGIN_VERSION"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$BINTRAY_PLUGIN_VERSION"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$DCENDENTS_MAVEN_PLUGIN_VERSION"

        // TODO Moving to Jitpack until the official 3.0.0 Release of android-maven-publish
        // (https://github.com/wupdigital/android-maven-publish/issues/2)
        // classpath "digital.wup:android-maven-publish:$ANDROID_MAVEN_PLUGIN_VERSION"
        classpath "com.github.wupdigital:android-maven-publish:$ANDROID_MAVEN_PLUGIN_VERSION"
    }
}

// Populate deployment credentials in an environment-aware fashion.
//
// * Local development:
//      Stored in local.properties file on the machine
// * CI Server:
//      Stored in environment variables before launch
Properties properties = new Properties()

def credentialsFile = new File(project.rootDir, "local.properties")
if (credentialsFile.exists()) {
    credentialsFile.withReader { properties.load(it) }
}

// @formatter:off
def bintrayUser     = properties.getProperty("BINTRAY_USER",    System.getenv("bintrayUser"))
def bintrayKey      = properties.getProperty("BINTRAY_KEY",     System.getenv("bintrayKey"))
def sonatypeUser    = properties.getProperty("SONATYPE_USER",   System.getenv("sonatypeUser"))
def sonatypePass    = properties.getProperty("SONATYPE_PASS",   System.getenv("sonatypePass"))
// @formatter:on

allprojects {
    repositories {
        google()
        jcenter()
    }

    // Store deployment credentials
    ext.bintrayUser = bintrayUser
    ext.bintrayKey = bintrayKey
    ext.sonatypeUser = sonatypeUser
    ext.sonatypePass = sonatypePass
}
