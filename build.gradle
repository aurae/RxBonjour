buildscript {
    repositories {
        jcenter()
        google()
        maven { url KOTLIN_REPO_URL }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$GRADLE_PLUGIN_VERSION"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$BINTRAY_PLUGIN_VERSION"
        classpath "digital.wup:android-maven-publish:$ANDROID_MAVEN_PLUGIN_VERSION"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

// Populate deployment credentials in an environment-aware fashion.
//
// * Local development:
//      Stored in local.properties file on the machine
// * CI Server:
//      Stored in environment variables before launch
Properties properties = new Properties()

def credentialsFile = new File(project.rootDir, "local.properties")
if (credentialsFile.exists()) {
    credentialsFile.withReader { properties.load(it) }
}

// @formatter:off
def bintrayUser     = properties.get("BINTRAY_USER",    System.getenv("bintrayUser"))
def bintrayKey      = properties.get("BINTRAY_KEY",     System.getenv("bintrayKey"))
// @formatter:on

allprojects {
    repositories {
        jcenter()
        google()
        maven { url KOTLIN_REPO_URL }
    }

    // Store deployment credentials
    ext.bintrayUser = bintrayUser
    ext.bintrayKey = bintrayKey
}
